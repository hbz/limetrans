plugins {
  id 'java'
  id 'checkstyle'
}

println "Timestamp: ${java.time.Instant.now().atZone(java.time.ZoneId.systemDefault())}, Host: ${java.net.InetAddress.getLocalHost()}"
println "Versions: JVM=${org.gradle.internal.jvm.Jvm.current()}, Groovy=${GroovySystem.getVersion()}, Gradle=${gradle.gradleVersion}"

group = 'hbz.limetrans'

def passSystemProperties = {
    System.properties.each { k, v ->
        if (k.startsWith(group + '.')) it[k] = v
    }
}

ext {
    versions = [
        'commons-cli'       : '1.3.1',
        'commons-io'        : '2.7',
        'commons-validator' : '1.5.1',
        'elasticsearch'     : '2.2.1',
        'jackson'           : '2.11.0',
        'jdk'               : '17',
        'junit'             : '4.13.1',
        'log4j'             : '2.5',
        'metafacture'       : '5.2.0'
    ]
}

apply from: 'gradle/tasks.gradle'

repositories {
    mavenCentral()
    maven { url = uri('https://oss.sonatype.org/content/repositories/snapshots') }
}

dependencies {
    implementation "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}"
    implementation "commons-cli:commons-cli:${versions['commons-cli']}"
    implementation "commons-io:commons-io:${versions['commons-io']}"
    implementation "commons-validator:commons-validator:${versions['commons-validator']}"
    implementation "junit:junit:${versions.junit}"
    implementation "org.apache.logging.log4j:log4j-1.2-api:${versions.log4j}"
    implementation "org.apache.logging.log4j:log4j-core:${versions.log4j}"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${versions.log4j}"
    implementation "org.elasticsearch:elasticsearch:${versions.elasticsearch}"
    implementation "org.metafacture:metafacture-biblio:${versions.metafacture}"
    implementation "org.metafacture:metafacture-commons:${versions.metafacture}"
    implementation "org.metafacture:metafacture-formeta:${versions.metafacture}"
    implementation "org.metafacture:metafacture-framework:${versions.metafacture}"
    implementation "org.metafacture:metafacture-io:${versions.metafacture}"
    implementation "org.metafacture:metafacture-javaintegration:${versions.metafacture}"
    implementation "org.metafacture:metafacture-json:${versions.metafacture}"
    implementation "org.metafacture:metafacture-mangling:${versions.metafacture}"
    implementation "org.metafacture:metafacture-plumbing:${versions.metafacture}"
    implementation "org.metafacture:metafacture-statistics:${versions.metafacture}"
    implementation "org.metafacture:metafacture-strings:${versions.metafacture}"
    implementation "org.metafacture:metafacture-xml:${versions.metafacture}"
    implementation "org.metafacture:metamorph:${versions.metafacture}"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(versions.jdk)
    }
}

checkstyle {
    toolVersion '8.30'
    checkstyleTest.enabled = false // TODO: Enable
}

tasks.withType(JavaCompile) {
    'all -processing -rawtypes -serial'.split().each {
        options.compilerArgs << "-Xlint:${it}"
    }

    options.compilerArgs << '-Werror'
}

tasks.withType(JavaExec) {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = java.toolchain.languageVersion
    }

    doFirst { passSystemProperties(systemProperties) }
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }

    passSystemProperties(systemProperties)

    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}
