def (root, extension, skel, required) = [rootProject.projectDir.toPath(), '.json', 'template', ['isil']]

def camelize = { it.replaceAll(~/\b./) { it[0].toUpperCase() } }
def taskName = { prefix, list -> prefix + list.collect { camelize(it) }.join('').replaceAll('[^a-zA-Z0-9]', '') }

def propTask = { group, list, prop = null ->
    prop = "${project.group}.${prop ?: group}"

    list.each { value ->
        task(taskName(group, [value]), group: group, description: "Sets -D${prop}=${value}") {
            doLast { System.properties[prop] = value }
        }
    }
}

// See hbz.limetrans.Main.Env.
propTask('env', ['prod', 'dev', 'd7test', 'local', 'ignore'])

// See hbz.limetrans.Limetrans.Type.
propTask('type', ['fix', 'morph'])

root.resolve('config').eachDir { path ->
    def group = path.getFileName().toString()

    def indexTask = { name, list ->
        task(taskName(group, [name]), type: JavaExec, group: group, description: list.collect { root.relativize(it) }) {
            classpath = sourceSets.main.runtimeClasspath
            mainClass = "${project.group}.Main"
            args list
        }
    }

    def newTask = { source ->
        def name = taskName(group, ['new'])
        def groupTask = project.tasks.findByName(name) ?:
            task(name, group: group, description: "Adds new configuration ${required}") {
                doLast {
                    def missing = required.findAll { !project.hasProperty(it) }
                    if (missing) throw new InvalidUserDataException(
                        'Missing ' + missing.collect { "-P${it}=..." })
                }
            }

        groupTask.doLast {
            copy {
                from source
                into path

                rename skel, isil.toLowerCase()
                expand(isil: isil)
            }
        }
    }

    path.eachFileMatch(~/.*\Q${extension}\E/) {
        def name = it.getFileName().toString().minus(extension)
        name.startsWith(skel) ? newTask(it) : indexTask(name, [it])
    }
}

[
    'bgzf'             : 'util.BGZF',
    'filter'           : 'filter.Main',
    'flux'             : 'org.metafacture.runner.Flux',
    'lmdb'             : 'util.LMDB',
    'sisis-supplement' : 'util.SisisSupplement',
    'transformation'   : 'Main',
    'verify-links'     : 'function.VerifyLinks'
].each { name, main ->
    task(taskName('exec', [name]), type: JavaExec, group: 'Exec', description: 'Execute ' + main + '.') {
        classpath = sourceSets.main.runtimeClasspath
        mainClass = main.count('.') > 1 ? main : "${project.group}.${main}"
        standardInput = System.in

        systemProperties['jdk.xml.totalEntitySizeLimit'] = 0
    }
}

def (matrixPairs, matrixProduct, matrixMultiply) = [[], []]
testMatrix.each { key, list ->
    list.add(0, '')
    matrixPairs << list.collect() { [key, it] }
}
matrixMultiply = { product, pairs ->
    def tail = pairs.tail()

    pairs.head().each {
        def list = product + [it]
        tail.isEmpty() ? matrixProduct << list : matrixMultiply(list, tail)
    }
}
matrixMultiply([], matrixPairs)

task matrix(group: 'Verification', description: 'Runs all checks for test matrix.')

matrixProduct.each {
    def (text, prop) = ["Matrix: ${it.collect { "${it[0]}=${it[1]}" }.join(' ')}", [:]]
    it.each { prop["${versionsProperty.prefix}${versionsProperty.separator}${it[0]}"] = it[1] }

    task(taskName('matrix', it.flatten()), type: GradleBuild, group: 'Verification', description: text) {
        startParameter.projectProperties = prop

        buildName = "[${text}]"
        tasks = ['check']

        matrix.dependsOn it
    }
}
